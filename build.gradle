buildscript {
    // Play Services version MUST be <= 16 (see chat with appodeal)
    // Other libraries also set to the older versions

    ext.ver_multidex='1.0.3' // managed by studio
    ext.ver_support = '27.1.1' // managed by studio
    ext.ver_takisoftPreferences = '27.1.1.1' // https://github.com/Gericop/Android-Support-Preference-V7-Fix
    ext.ver_playServices = '16.0.0' // managed by studio
    ext.ver_appCenter = '1.10.0' // https://docs.microsoft.com/en-us/appcenter/sdk/getting-started/android
    ext.ver_processPhoenix = '2.0.0' // https://github.com/JakeWharton/ProcessPhoenix

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        flatDir { dirs 'src/main/libs-aar/appodeal' }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:all'
        options.compilerArgs << '-Xlint:-deprecation'
        options.compilerArgs << '-Xlint:-unchecked'
    }
}

// task clean(type: Delete) {
//     delete rootProject.buildDir
// }

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16

        //noinspection OldTargetApi
        targetSdkVersion 27

        multiDexEnabled true

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)

        applicationId 'com.eightsines.firestrike'

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    if (new File('tools/signing.properties').exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file('tools/signing.properties')))

        signingConfigs {
            release {
                storeFile file('tools/signing.keystore')
                keyAlias props['keyAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

dependencies {
    //
    // Game
    //

    implementation "com.android.support:multidex:${ver_multidex}"
    implementation "com.android.support:appcompat-v7:${ver_support}"
    implementation "com.android.support:support-v4:${ver_support}"
    implementation "com.jakewharton:process-phoenix:${ver_processPhoenix}"

    //noinspection GradleDependency
    implementation "com.takisoft.fix:preference-v7:${ver_takisoftPreferences}"

    //
    // Analytics and crash-reporting
    //

    implementation "com.microsoft.appcenter:appcenter-analytics:${ver_appCenter}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${ver_appCenter}"

    //
    // Appodeal
    //

    //noinspection GradleDependency
    implementation("com.google.android.gms:play-services-ads:${ver_playServices}") {
        exclude group: 'com.android.support', module: 'animated-vector-drawable'
        exclude group: 'com.android.support', module: 'customtabs'
    }

    implementation "com.google.android.gms:play-services-location:${ver_playServices}"

    implementation fileTree(dir: 'src/main/libs/appodeal', include: [ '*.jar' ])
    implementation name: 'adcolony-3.3.4', ext: 'aar'
    implementation name: 'mobvista-8.11.3-alphab', ext: 'aar'
    implementation name: 'mobvista-8.11.3-common', ext: 'aar'
    implementation name: 'mobvista-8.11.3-interstitial', ext: 'aar'
    implementation name: 'mobvista-8.11.3-interstitialvideo', ext: 'aar'
    implementation name: 'mobvista-8.11.3-mvdownloads', ext: 'aar'
    implementation name: 'mobvista-8.11.3-mvjscommon', ext: 'aar'
    implementation name: 'mobvista-8.11.3-mvnative', ext: 'aar'
    implementation name: 'mobvista-8.11.3-nativeex', ext: 'aar'
    implementation name: 'mobvista-8.11.3-playercommon', ext: 'aar'
    implementation name: 'mobvista-8.11.3-reward', ext: 'aar'
    implementation name: 'mobvista-8.11.3-videocommon', ext: 'aar'
    implementation name: 'mobvista-8.11.3-videofeeds', ext: 'aar'
    implementation name: 'mobvista-8.11.3-videojs', ext: 'aar'
    implementation name: 'ogury-3.0.13', ext: 'aar'
}
