buildscript {
    ext.ver_androidx_appcompat = '1.2.0' // managed by studio
    ext.ver_androidx_preference = '1.1.1' // managed by studio
    ext.ver_takisoftPreferences = '1.1.0' // https://github.com/Gericop/Android-Support-Preference-V7-Fix
    ext.ver_processPhoenix = '2.0.0' // https://github.com/JakeWharton/ProcessPhoenix

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:all'
        options.compilerArgs << '-Xlint:-deprecation'
        options.compilerArgs << '-Xlint:-unchecked'
    }
}

task cleanfix(type: Delete) {
    delete rootProject.buildDir
    delete "${rootProject.rootDir}/.cxx"
    delete "${rootProject.rootDir}/.externalNativeBuild"
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)

        applicationId 'com.eightsines.firestrike.opensource'

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    if (new File('_signing/signing.properties').exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file('_signing/signing.properties')))

        signingConfigs {
            release {
                storeFile file('_signing/signing.keystore')
                keyAlias props['keyAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:${ver_androidx_appcompat}"
    implementation "androidx.preference:preference:${ver_androidx_preference}"
    implementation "com.jakewharton:process-phoenix:${ver_processPhoenix}"
    implementation "com.takisoft.preferencex:preferencex:${ver_takisoftPreferences}"
}
